#version 460

#include "core/struct.glsl"
layout(local_size_x = 32) in;

layout(set = 0, binding = 0) buffer writeonly LightHitUbo {
  HitRecord records[];
} lightHitUbo;

layout(set = 0, binding = 1) buffer readonly LightRayUbo {
  RayData rayDatas[];
} lightRayUbo;

layout(set = 0, binding = 2) buffer readonly LightSsbo {
  TriangleLight lights[];
};

layout(set = 0, binding = 3) buffer readonly LightBvhSsbo {
  BvhNode lightBvhNodes[];
};

layout(set = 0, binding = 4) buffer readonly VertexSsbo {
  Vertex vertices[];
};

#define KEPSILON 0.00001

// ------------- Basic -------------

vec3 rayAt(Ray r, float t) {
  return r.origin + t * r.direction;
}

vec3 setFaceNormal(vec3 r_direction, vec3 outwardNormal) {
  return dot(normalize(r_direction), outwardNormal) < 0.0f ? outwardNormal : -1.0f * outwardNormal;
}

// ------------- Triangle Light -------------

HitRecord hitTriangleLight(uvec3 triIndices, Ray r, float dirMin, vec3 dirMax) {
  HitRecord hit;
  hit.isHit = 0.0f;

  vec3 v0v1 = vertices[triIndices.y].position - vertices[triIndices.x].position;
  vec3 v0v2 = vertices[triIndices.z].position - vertices[triIndices.x].position;
  vec3 pvec = cross(r.direction, v0v2);
  float det = dot(v0v1, pvec);
  
#ifdef BACKFACE_CULLING
  if (det < KEPSILON) {
    return hit;
  }
#else
  if (abs(det) < KEPSILON) {
    return hit;
  }
#endif

  vec3 tvec = r.origin - vertices[triIndices.x].position;
  float u = dot(tvec, pvec) / det;
  if (u < 0.0f || u > 1.0f) {
    return hit;
  }

  vec3 qvec = cross(tvec, v0v1);
  float v = dot(r.direction, qvec) / det;
  if (v < 0.0f || u + v > 1.0f) {
    return hit;
  }
  
  float t = dot(v0v2, qvec) / det;
  vec3 dir = t * r.direction;

  if (length(dir) < dirMin || length(dir) > length(dirMax)) {
    return hit;
  }

  hit.isHit = 1.0f;
  hit.dir = dir;
  hit.point = rayAt(r, t);

  vec3 outwardNormal = normalize(cross(v0v1, v0v2));
  hit.normal = setFaceNormal(r.direction, outwardNormal);

  return hit;
}

// ------------- Point Light -------------

HitRecord hitPointLight(PointLight light, Ray r, float dirMin, vec3 dirMax) {
  HitRecord hit;
  hit.isHit = 0.0f;

  vec3 lightDirection = light.position - r.origin;
  vec3 lightNormal = normalize(lightDirection);

  if (dot(normalize(r.direction), lightNormal) < 0.99f || length(lightDirection) < dirMin || length(lightDirection) > length(dirMax)) {
    return hit;
  }

  hit.isHit = 1.0f;
  hit.dir = lightDirection;
  hit.point = light.position;
  hit.normal = setFaceNormal(r.direction, lightNormal);

  return hit;
}

// ------------- Bvh -------------

bool intersectAABB(Ray r, vec3 boxMin, vec3 boxMax) {
  vec3 tMin = (boxMin - r.origin) / r.direction;
  vec3 tMax = (boxMax - r.origin) / r.direction;
  vec3 t1 = min(tMin, tMax);
  vec3 t2 = max(tMin, tMax);
  float tNear = max(max(t1.x, t1.y), t1.z);
  float tFar = min(min(t2.x, t2.y), t2.z);

  return tNear < tFar;
}

// ------------- Triangle Light-------------

HitRecord hitTriangleLightBvh(Ray r, float dirMin, vec3 dirMax) {
  HitRecord hit;
  hit.isHit = 0.0f;
  hit.dir = dirMax;

  uint stack[30];
  stack[0] = 1u;

  int stackIndex = 1;
  while(stackIndex > 0 && stackIndex <= 30) {
    stackIndex--;
    uint currentNode = stack[stackIndex];
    if (currentNode == 0u) {
      continue;
    }

    if (!intersectAABB(r, lightBvhNodes[currentNode - 1u].minimum, lightBvhNodes[currentNode - 1u].maximum)) {
      continue;
    }

    uint lightIndex = lightBvhNodes[currentNode - 1u].leftObjIndex;
    if (lightIndex >= 1u) {
      HitRecord tempHit = hitTriangleLight(lights[lightIndex - 1u].indices, r, dirMin, hit.dir);

      if (tempHit.isHit == 1.0f) {
        hit = tempHit;
        hit.hitIndex = lightIndex - 1u;
      }
    }

    lightIndex = lightBvhNodes[currentNode - 1u].rightObjIndex;    
    if (lightIndex >= 1u) {
      HitRecord tempHit = hitTriangleLight(lights[lightIndex - 1u].indices, r, dirMin, hit.dir);

      if (tempHit.isHit == 1.0f) {
        hit = tempHit;
        hit.hitIndex = lightIndex - 1u;
      }
    }

    uint bvhNode = lightBvhNodes[currentNode - 1u].leftNode;
    if (bvhNode >= 1u) {
      stack[stackIndex] = bvhNode;
      stackIndex++;
    }

    bvhNode = lightBvhNodes[currentNode - 1u].rightNode;
    if (bvhNode >= 1u) {
      stack[stackIndex] = bvhNode;
      stackIndex++;
    }
  }

  return hit;
}

void main() {
  RayData rayData = lightRayUbo.rayDatas[gl_GlobalInvocationID.x];
  HitRecord hitRecord;
  
  hitRecord.isHit = 0.0f;
  hitRecord.hitIndex = 0u;
  hitRecord.materialIndex = 0u;
  hitRecord.point = vec3(0.0f);
  hitRecord.dir = vec3(0.0f);
  hitRecord.normal = vec3(0.0f);
  hitRecord.isPrimary = rayData.isPrimary;

  if (length(rayData.ray.direction) > 0.1f) {
    hitRecord = hitTriangleLightBvh(rayData.ray, rayData.dirMin, rayData.dirMax);
  }

  lightHitUbo.records[gl_GlobalInvocationID.x] = hitRecord;
}