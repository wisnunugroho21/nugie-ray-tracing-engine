#version 460

#include "core/struct.glsl"
#include "core/boolean.glsl"

layout(local_size_x = 32) in;

layout(set = 0, binding = 0, rgba8) uniform image2D resultImage;

layout(set = 0, binding = 1) buffer IndirectSamplerDataUbo {
  IndirectSamplerData datas[];
} indirectSamplerDataUbo;

layout(set = 0, binding = 2) buffer RenderResultUbo {
  RenderResult datas[];
} renderResultUbo;

layout(set = 0, binding = 3) buffer readonly MissUbo {
  MissRecord records[];
} missUbo;

layout(set = 0, binding = 4) buffer readonly LightShadeUbo {
  LightShadeRecord records[];
} lightShadeUbo;

layout(set = 0, binding = 5) buffer readonly IndirectShadeUbo {
  IndirectShadeRecord records[];
} indirectShadeUbo;

layout(set = 0, binding = 6) buffer readonly DirectShadeUbo {
  DirectShadeRecord records[];
} directShadeUbo;

void main() {
  IndirectSamplerData samplerData = indirectSamplerDataUbo.datas[gl_GlobalInvocationID.x];
  DirectShadeRecord directRecord = directShadeUbo.records[gl_GlobalInvocationID.x];
  IndirectShadeRecord indirectRecord = indirectShadeUbo.records[gl_GlobalInvocationID.x];
  LightShadeRecord lightShadeRecord = lightShadeUbo.records[gl_GlobalInvocationID.x];
  MissRecord missRecord = missUbo.records[gl_GlobalInvocationID.x];
  RenderResult prevRenderResult = renderResultUbo.datas[gl_GlobalInvocationID.x];

  ivec2 pixelCoord = ivec2(samplerData.xCoord, samplerData.yCoord);
  vec3 totalPrevIndirect = prevRenderResult.totalIndirect / prevRenderResult.pdf;

  vec3 curRadiance = lightShadeRecord.radiance * prevRenderResult.totalIndirect * float(lightShadeRecord.isIlluminate);
  curRadiance += missRecord.radiance * totalPrevIndirect * float(missRecord.isMiss);
  curRadiance += directRecord.radiance * totalPrevIndirect * float(directRecord.isIlluminate);

  float totalPdf = indirectRecord.pdf + directRecord.pdf;

  vec3 totalRadiance = prevRenderResult.totalRadiance * float(lightShadeRecord.rayBounce > 0u) + curRadiance * directRecord.pdf / totalPdf;
  vec3 totalIndirect = totalPrevIndirect * indirectRecord.radiance * indirectRecord.pdf / totalPdf;
  bool isRayContinue = indirectRecord.isIlluminate && (lightShadeRecord.rayBounce < 3u) && (length(totalIndirect) > 0.1f);

  if (isRayContinue) {
    imageStore(resultImage, pixelCoord, vec4(totalRadiance, 1.0f));
  }

  RenderResult renderResult;
  renderResult.totalRadiance = mix(vec3(0.0f), totalRadiance, float(isRayContinue));
  renderResult.totalIndirect = mix(vec3(1.0f), totalIndirect, float(isRayContinue));
  renderResult.pdf = mix(1.0f, indirectRecord.pdf, float(isRayContinue));

  renderResultUbo.datas[gl_GlobalInvocationID.x] = renderResult;

  samplerData.rayBounce = (lightShadeRecord.rayBounce + 1u) * uint(isRayContinue);
  samplerData.nextRay = indirectRecord.nextRay;
  
  indirectSamplerDataUbo.datas[gl_GlobalInvocationID.x] = samplerData;
}