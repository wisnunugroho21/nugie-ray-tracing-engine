#version 460

#include "core/struct.glsl"

layout(local_size_x = 32) in;

layout(set = 0, binding = 0) uniform readonly GlobalUniform {
  vec3 origin;
  vec3 horizontal;
  vec3 vertical;
  vec3 lowerLeftCorner;
  uvec2 imgSize;
  uint numLights;
  SunLight sunLight;
  vec3 skyColor;
} ubo;

layout(set = 0, binding = 1) buffer writeonly DirectBuffer {
  DirectShadeRecord records[];
} directBuffer;

layout(set = 0, binding = 2) buffer readonly ObjectHitBuffer {
  HitRecord records[];
} objectHitBuffer;

layout(set = 0, binding = 3) buffer readonly LightHitBuffer {
  HitRecord records[];
} lightHitBuffer;

layout(set = 0, binding = 4) buffer readonly DirectDataBuffer {
  DirectData datas[];
} directDataBuffer;

layout(set = 0, binding = 5) buffer readonly MaterialModel {
  Material materials[];
};

layout(push_constant) uniform Push {
  uint randomSeed;
} push;

// ------------- Basic -------------

float maxComponent(vec3 v) {
  return max(v.x, max(v.y, v.z));
}

// ------------- Integrand ------------- 

vec3 integrandOverHemisphere(vec3 color, float brdf, float NoL, float pdf) {
  return color * brdf * NoL / pdf; 
}

vec3 integrandOverArea(vec3 color, float brdf, float NoL, float NloL, float squareDistance, float area) {
  return color * brdf * NoL * NloL * area / squareDistance;
}

vec3 partialIntegrand(vec3 color, float brdf, float NoL) {
  return color * brdf * NoL;
}

float Gfactor(float NloL, float squareDistance, float area) {
  return NloL * area / squareDistance;
}

// ------------- Lambert ------------- 

float lambertPdfValue(float NoL) {
  return NoL / pi;
}

float lambertBrdfValue() {
  return 1.0f / pi;
}

void main() {
  HitRecord objectHit = objectHitBuffer.records[gl_GlobalInvocationID.x];
  HitRecord lightHit = lightHitBuffer.records[gl_GlobalInvocationID.x];
  DirectData directData = directDataBuffer.datas[gl_GlobalInvocationID.x];

  Material surfaceMaterial = materials[directData.materialIndex];

  DirectShadeRecord directShadeResult;
  directShadeResult.isIlluminate = directData.isIlluminate && !objectHit.isHit && !lightHit.isHit;
  directShadeResult.radiance = vec3(0.0f);
  directShadeResult.pdf = 0.0f;

  if (directShadeResult.isIlluminate) {
    vec3 unitLightDirection = normalize(ubo.sunLight.direction);
      
    float NoL = max(dot(directData.normal, unitLightDirection), 0.01f);
    float brdf = lambertBrdfValue();
    float pdf = lambertPdfValue(NoL);

    directShadeResult.radiance = ubo.sunLight.color * surfaceMaterial.baseColor * brdf * NoL / pdf;
    directShadeResult.pdf = maxComponent(directShadeResult.radiance) > 0.00001f ? pdf : 0.0f;
  }
  
  directBuffer.records[gl_GlobalInvocationID.x] = directShadeResult;
}