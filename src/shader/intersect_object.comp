#version 460

#include "core/struct.glsl"
layout(local_size_x = 32) in;

layout(set = 0, binding = 0) buffer writeonly ObjectHitBuffer {
  HitRecord records[];
} objectHitBuffer;

layout(set = 0, binding = 1) buffer readonly ObjectRayBuffer {
  RayData rayDatas[];
} objectRayBuffer;

layout(set = 0, binding = 2) buffer readonly ObjectModel {
  Object objects[];
};

layout(set = 0, binding = 3) buffer readonly ObjectBvhModel {
  BvhNode objectBvhNodes[];
};

layout(set = 0, binding = 4) buffer readonly PrimitiveModel {
  Primitive primitives[];
};

layout(set = 0, binding = 5) buffer readonly PrimitiveBvhModel {
  BvhNode primitiveBvhNodes[];
};

layout(set = 0, binding = 6) buffer readonly VertexModel {
  Vertex vertices[];
};

layout(set = 0, binding = 7) buffer readonly MaterialModel {
  Material materials[];
};

layout(set = 0, binding = 8) buffer readonly TransformationModel {
  Transformation transformations[];
};

layout(set = 0, binding = 9) uniform sampler2D normalTextureSampler[1];

#define KEPSILON 0.00001

// ------------- Basic -------------

vec3 rayAt(Ray r, float t) {
  return r.origin + t * r.direction;
}

vec3 setFaceNormal(vec3 r_direction, vec3 outwardNormal) {
  return dot(normalize(r_direction), outwardNormal) < 0.0f ? outwardNormal : -1.0f * outwardNormal;
}

vec2 getTotalTextureCoordinate(uvec3 triIndices, vec2 uv) {
  return (1.0f - uv.x - uv.y) * vertices[triIndices.x].textCoord + uv.x * vertices[triIndices.y].textCoord + uv.y * vertices[triIndices.z].textCoord;
}

// ------------- Triangle -------------

HitRecord hitTriangle(uvec3 triIndices, Ray r, float dirMin, vec3 dirMax, uint transformIndex, uint materialIndex) {
  HitRecord hit;
  hit.isHit = false;

  vec3 v0v1 = vertices[triIndices.y].position - vertices[triIndices.x].position;
  vec3 v0v2 = vertices[triIndices.z].position - vertices[triIndices.x].position;
  vec3 pvec = cross(r.direction, v0v2);
  float det = dot(v0v1, pvec);
  
#ifdef BACKFACE_CULLING
  if (det < KEPSILON) {
    return hit;
  }
#else
  if (abs(det) < KEPSILON) {
    return hit;
  }
#endif

  vec3 tvec = r.origin - vertices[triIndices.x].position;
  float u = dot(tvec, pvec) / det;
  if (u < 0.0f || u > 1.0f) {
    return hit;
  }

  vec3 qvec = cross(tvec, v0v1);
  float v = dot(r.direction, qvec) / det;
  if (v < 0.0f || u + v > 1.0f) {
    return hit;
  }
  
  float t = dot(v0v2, qvec) / det;
  vec3 dir = mat3(transformations[transformIndex].dirMatrix) * t * r.direction;

  if (length(dir) < dirMin || length(dir) > length(dirMax)) {
    return hit;
  }

  hit.isHit = true;
  hit.dir = dir;
  hit.materialIndex = materialIndex;
  hit.uv = getTotalTextureCoordinate(triIndices, vec2(u, v));
  hit.point = (transformations[transformIndex].pointMatrix * vec4(rayAt(r, t), 1.0f)).xyz;

  if (materials[materialIndex].normalTextureIndex == 0u) {
    vec3 outwardNormal = normalize(cross(v0v1, v0v2));
    hit.normal = normalize(mat3(transformations[transformIndex].normalMatrix) * setFaceNormal(r.direction, outwardNormal));
  } else {
    vec3 outwardNormal = texture(normalTextureSampler[materials[materialIndex].normalTextureIndex - 1u], hit.uv).xyz;
    hit.normal = normalize(mat3(transformations[transformIndex].normalMatrix) * outwardNormal);
  }

  return hit;
}

// ------------- Bvh -------------

float intersectAABB(Ray r, vec3 boxMin, vec3 boxMax) {
  vec3 tMin = (boxMin - r.origin) / r.direction;
  vec3 tMax = (boxMax - r.origin) / r.direction;
  vec3 t1 = min(tMin, tMax);
  vec3 t2 = max(tMin, tMax);
  float tNear = max(max(t1.x, t1.y), t1.z);
  float tFar = min(min(t2.x, t2.y), t2.z);

  return tNear <= tFar ? tNear : 1000000000000.0f;
}

HitRecord hitPrimitiveBvh(Ray r, float dirMin, vec3 dirMax, uint firstBvhIndex, uint firstPrimitiveIndex, uint transformIndex) {
  HitRecord hit;
  hit.isHit = false;
  hit.dir = dirMax;

  r.origin = (transformations[transformIndex].pointInverseMatrix * vec4(r.origin, 1.0f)).xyz;
  r.direction = mat3(transformations[transformIndex].dirInverseMatrix) * r.direction;

  if (intersectAABB(r, primitiveBvhNodes[firstBvhIndex].minimum, primitiveBvhNodes[firstBvhIndex].maximum) == 1000000000000.0f) {
    return hit;
  }

  uint stack[32];
  stack[0] = 1u;

  int stackIndex = 1;

  while(stackIndex > 0 && stackIndex <= 30) {
    stackIndex--;
    uint currentNode = stack[stackIndex];
    if (currentNode == 0u) {
      continue;
    }

    uint primIndex = primitiveBvhNodes[currentNode - 1u + firstBvhIndex].leftObjIndex;
    if (primIndex >= 1u) {
      HitRecord tempHit = hitTriangle(primitives[primIndex - 1u + firstPrimitiveIndex].indices, r, dirMin, hit.dir, transformIndex, primitives[primIndex - 1u + firstPrimitiveIndex].materialIndex);

      if (tempHit.isHit) {
        hit = tempHit;
        hit.hitIndex = primIndex - 1u + firstPrimitiveIndex;
      }
    }

    primIndex = primitiveBvhNodes[currentNode - 1u + firstBvhIndex].rightObjIndex;    
    if (primIndex >= 1u) {
      HitRecord tempHit = hitTriangle(primitives[primIndex - 1u + firstPrimitiveIndex].indices, r, dirMin, hit.dir, transformIndex, primitives[primIndex - 1u + firstPrimitiveIndex].materialIndex);

      if (tempHit.isHit) {
        hit = tempHit;
        hit.hitIndex = primIndex - 1u + firstPrimitiveIndex;
      }
    }

    uint leftNode = primitiveBvhNodes[currentNode - 1u + firstBvhIndex].leftNode;
    uint rightNode = primitiveBvhNodes[currentNode - 1u + firstBvhIndex].rightNode;

    if (leftNode == 0u && rightNode == 0u) {
      continue;
    }

    if (leftNode != 0u && rightNode == 0u) {
      float leftDist = intersectAABB(r, primitiveBvhNodes[leftNode - 1u + firstBvhIndex].minimum, primitiveBvhNodes[leftNode - 1u + firstBvhIndex].maximum);

      if (leftDist < 1000000000000.0f) {
        stack[stackIndex] = leftNode;
        stackIndex++;
      }

      continue;
    }

    if (leftNode == 0u && rightNode != 0u) {
      float rightDist = intersectAABB(r, primitiveBvhNodes[rightNode - 1u + firstBvhIndex].minimum, primitiveBvhNodes[rightNode - 1u + firstBvhIndex].maximum);

      if (rightDist < 1000000000000.0f) {
        stack[stackIndex] = rightNode;
        stackIndex++;
      }
      
      continue;
    }

    float leftDist = intersectAABB(r, primitiveBvhNodes[leftNode - 1u + firstBvhIndex].minimum, primitiveBvhNodes[leftNode - 1u + firstBvhIndex].maximum);
    float rightDist = intersectAABB(r, primitiveBvhNodes[rightNode - 1u + firstBvhIndex].minimum, primitiveBvhNodes[rightNode - 1u + firstBvhIndex].maximum);

    if (leftDist == 1000000000000.0f && rightDist == 1000000000000.0f) {
      continue;
    }
    
    if (leftDist < 1000000000000.0f && rightDist == 1000000000000.0f) {
      stack[stackIndex] = leftNode;
      stackIndex++;
      continue;
    }

    if (leftDist == 1000000000000.0f && rightDist < 1000000000000.0f) {
      stack[stackIndex] = rightNode;
      stackIndex++;
      continue;
    }

    if (leftDist <= rightDist) {
      stack[stackIndex] = leftNode;
      stackIndex++;

      stack[stackIndex] = rightNode;
      stackIndex++;
    } else if (leftDist > rightDist) {
      stack[stackIndex] = rightNode;
      stackIndex++;

      stack[stackIndex] = leftNode;
      stackIndex++;
    }
  }

  return hit;
}

HitRecord hitObjectBvh(Ray r, float dirMin, vec3 dirMax) {
  HitRecord hit;
  hit.isHit = false;
  hit.dir = dirMax;

  uint stack[30];
  stack[0] = 1u;

  int stackIndex = 1;
  while(stackIndex > 0 && stackIndex <= 30) {
    stackIndex--;
    uint currentNode = stack[stackIndex];
    if (currentNode == 0u) {
      continue;
    }

    if (intersectAABB(r, objectBvhNodes[currentNode - 1u].minimum, objectBvhNodes[currentNode - 1u].maximum) == 1000000000000.0f) {
      continue;
    }

    uint objIndex = objectBvhNodes[currentNode - 1u].leftObjIndex;
    if (objIndex >= 1u) {
      HitRecord tempHit = hitPrimitiveBvh(r, dirMin, hit.dir, objects[objIndex - 1u].firstBvhIndex, objects[objIndex - 1u].firstPrimitiveIndex, objects[objIndex - 1u].transformIndex);

      if (tempHit.isHit) {
        hit = tempHit;
      }
    }

    objIndex = objectBvhNodes[currentNode - 1u].rightObjIndex;
    if (objIndex >= 1u) {
      HitRecord tempHit = hitPrimitiveBvh(r, dirMin, hit.dir, objects[objIndex - 1u].firstBvhIndex, objects[objIndex - 1u].firstPrimitiveIndex, objects[objIndex - 1u].transformIndex);

      if (tempHit.isHit) {
        hit = tempHit;
      }
    }

    uint bvhNode = objectBvhNodes[currentNode - 1u].leftNode;
    if (bvhNode >= 1u) {
      stack[stackIndex] = bvhNode;
      stackIndex++;
    }

    bvhNode = objectBvhNodes[currentNode - 1u].rightNode;
    if (bvhNode >= 1u) {
      stack[stackIndex] = bvhNode;
      stackIndex++;
    }
  }

  return hit;
}

void main() {
  RayData rayData = objectRayBuffer.rayDatas[gl_GlobalInvocationID.x];
  HitRecord hitRecord;
  
  hitRecord.isHit = false;
  hitRecord.hitIndex = 0u;
  hitRecord.materialIndex = 0u;
  hitRecord.point = vec3(0.0f);
  hitRecord.dir = vec3(0.0f);
  hitRecord.normal = vec3(0.0f);
  
  if (length(rayData.ray.direction) > 0.1f) {
    hitRecord = hitObjectBvh(rayData.ray, rayData.dirMin, rayData.dirMax);
  }

  hitRecord.rayBounce = rayData.rayBounce;
  objectHitBuffer.records[gl_GlobalInvocationID.x] = hitRecord;
}