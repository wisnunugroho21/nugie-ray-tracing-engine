#version 460

#include "core/struct.glsl"
layout(local_size_x = 32) in;

layout(set = 0, binding = 0) buffer writeonly ObjectHitBuffer {
  HitRecord records[];
} objectHitBuffer;

layout(set = 0, binding = 1) buffer readonly ObjectRayBuffer {
  RayData rayDatas[];
} objectRayBuffer;

layout(set = 0, binding = 2) buffer readonly ObjectModel {
  Object objects[];
};

layout(set = 0, binding = 3) buffer readonly ObjectBvhModel {
  BvhNode objectBvhNodes[];
};

layout(set = 0, binding = 4) buffer readonly PrimitiveModel {
  Primitive primitives[];
};

layout(set = 0, binding = 5) buffer readonly PrimitiveBvhModel {
  BvhNode primitiveBvhNodes[];
};

layout(set = 0, binding = 6) buffer readonly VertexModel {
  Vertex vertices[];
};

layout(set = 0, binding = 7) buffer readonly MaterialModel {
  Material materials[];
};

layout(set = 0, binding = 8) buffer readonly TransformationModel {
  Transformation transformations[];
};

#define KEPSILON 0.00001

// ------------- Basic -------------

vec3 rayAt(Ray r, float t) {
  return r.origin + t * r.direction;
}

vec3 setFaceNormal(vec3 r_direction, vec3 outwardNormal) {
  return dot(normalize(r_direction), outwardNormal) < 0.0f ? outwardNormal : -1.0f * outwardNormal;
}

vec2 getTotalTextureCoordinate(uvec3 triIndices, vec2 uv) {
  return (1.0f - uv.x - uv.y) * vertices[triIndices.x].textCoord + uv.x * vertices[triIndices.y].textCoord + uv.y * vertices[triIndices.z].textCoord;
}

// ------------- Triangle -------------

HitRecord hitTriangle(uvec3 triIndices, Ray r, float dirMin, vec3 dirMax, uint transformIndex, uint materialIndex) {
  HitRecord hit;
  hit.isHit = false;

  vec3 v0v1 = vertices[triIndices.y].position - vertices[triIndices.x].position;
  vec3 v0v2 = vertices[triIndices.z].position - vertices[triIndices.x].position;
  vec3 pvec = cross(r.direction, v0v2);
  float det = dot(v0v1, pvec);
  
#ifdef BACKFACE_CULLING
  if (det < KEPSILON) {
    return hit;
  }
#else
  if (abs(det) < KEPSILON) {
    return hit;
  }
#endif

  vec3 tvec = r.origin - vertices[triIndices.x].position;
  float u = dot(tvec, pvec) / det;
  if (u < 0.0f || u > 1.0f) {
    return hit;
  }

  vec3 qvec = cross(tvec, v0v1);
  float v = dot(r.direction, qvec) / det;
  if (v < 0.0f || u + v > 1.0f) {
    return hit;
  }
  
  float t = dot(v0v2, qvec) / det;
  vec3 dir = mat3(transformations[transformIndex].dirMatrix) * t * r.direction;

  if (length(dir) < dirMin || length(dir) > length(dirMax)) {
    return hit;
  }

  hit.isHit = true;
  hit.dir = dir;
  hit.materialIndex = materialIndex;
  hit.uv = getTotalTextureCoordinate(triIndices, vec2(u, v));
  hit.point = (transformations[transformIndex].pointMatrix * vec4(rayAt(r, t), 1.0f)).xyz;

  vec3 outwardNormal = normalize(cross(v0v1, v0v2));
  hit.normal = normalize(mat3(transformations[transformIndex].normalMatrix) * setFaceNormal(r.direction, outwardNormal));

  return hit;
}

// ------------- Bvh -------------

float intersectAABB(Ray r, vec3 boxMin, vec3 boxMax) {
  vec3 tMin = (boxMin - r.origin) / r.direction;
  vec3 tMax = (boxMax - r.origin) / r.direction;
  vec3 t1 = min(tMin, tMax);
  vec3 t2 = max(tMin, tMax);
  float tNear = max(max(t1.x, t1.y), t1.z);
  float tFar = min(min(t2.x, t2.y), t2.z);

  return tNear <= tFar ? tNear : 1000000000000.0f;
}

HitRecord hitPrimitiveBvh(Ray r, float dirMin, vec3 dirMax, uint firstBvhIndex, uint firstPrimitiveIndex, uint transformIndex) {
  HitRecord hit;
  hit.isHit = false;
  hit.dir = dirMax;

  Transformation curTransf = transformations[transformIndex];
  BvhNode rootNode = primitiveBvhNodes[firstBvhIndex];

  r.origin = (curTransf.pointInverseMatrix * vec4(r.origin, 1.0f)).xyz;
  r.direction = mat3(curTransf.dirInverseMatrix) * r.direction;

  if (intersectAABB(r, rootNode.minimum, rootNode.maximum) >= 1000000000000.0f) {
    return hit;
  }

  uint stack[32];
  stack[0] = 1u;

  int stackIndex = 1;

  while(stackIndex > 0 && stackIndex <= 30) {
    stackIndex--;
    uint currentNode = stack[stackIndex];
    if (currentNode == 0u) {
      continue;
    }

    BvhNode curNode = primitiveBvhNodes[currentNode - 1u + firstBvhIndex];

    uint primIndex = curNode.leftObjIndex;
    if (primIndex >= 1u) {
      Primitive leftPrimitive = primitives[primIndex - 1u + firstPrimitiveIndex]; 
      HitRecord tempHit = hitTriangle(leftPrimitive.indices, r, dirMin, hit.dir, transformIndex, leftPrimitive.materialIndex);

      if (tempHit.isHit) {
        hit = tempHit;
        hit.hitIndex = primIndex - 1u + firstPrimitiveIndex;
      }
    }

    primIndex = curNode.rightObjIndex;    
    if (primIndex >= 1u) {
      Primitive rightPrimitive = primitives[primIndex - 1u + firstPrimitiveIndex];
      HitRecord tempHit = hitTriangle(rightPrimitive.indices, r, dirMin, hit.dir, transformIndex, rightPrimitive.materialIndex);

      if (tempHit.isHit) {
        hit = tempHit;
        hit.hitIndex = primIndex - 1u + firstPrimitiveIndex;
      }
    }

    uint leftNodeIndex = curNode.leftNode;
    uint rightNodeIndex = curNode.rightNode;

    float leftDist = 1000000000000.0f;
    float rightDist = 1000000000000.0f;

    if (leftNodeIndex > 0u) {
      BvhNode leftNode = primitiveBvhNodes[leftNodeIndex - 1u + firstBvhIndex];
      leftDist = intersectAABB(r, leftNode.minimum, leftNode.maximum);
    }

    if (rightNodeIndex > 0u) {
      BvhNode rightNode = primitiveBvhNodes[rightNodeIndex - 1u + firstBvhIndex];
      rightDist = intersectAABB(r, rightNode.minimum, rightNode.maximum);
    }

    if (leftDist >= 1000000000000.0f) {
      if (rightDist < 1000000000000.0f) {
        stack[stackIndex] = rightNodeIndex;
        stackIndex++;
      }

      continue;
    }

    if (rightDist >= 1000000000000.0f) {
      stack[stackIndex] = leftNodeIndex;
      stackIndex++;
      continue;
    }

    if (leftDist <= rightDist) {
      stack[stackIndex] = rightNodeIndex;
      stackIndex++;

      stack[stackIndex] = leftNodeIndex;
      stackIndex++;
    } else {
      stack[stackIndex] = leftNodeIndex;
      stackIndex++;

      stack[stackIndex] = rightNodeIndex;
      stackIndex++;
    }
  }

  return hit;
}

HitRecord hitObjectBvh(Ray r, float dirMin, vec3 dirMax) {
  HitRecord hit;
  hit.isHit = false;
  hit.dir = dirMax;

  BvhNode rootNode = objectBvhNodes[0u];
  if (intersectAABB(r, rootNode.minimum, rootNode.maximum) >= 1000000000000.0f) {
    return hit;
  }

  uint stack[30];
  stack[0] = 1u;

  int stackIndex = 1;
  while(stackIndex > 0 && stackIndex <= 30) {
    stackIndex--;
    uint currentNode = stack[stackIndex];
    if (currentNode == 0u) {
      continue;
    }

    BvhNode curNode = objectBvhNodes[currentNode - 1u];

    uint objIndex = curNode.leftObjIndex;
    if (objIndex >= 1u) {
      Object leftObject = objects[objIndex - 1u];
      HitRecord tempHit = hitPrimitiveBvh(r, dirMin, hit.dir, leftObject.firstBvhIndex, leftObject.firstPrimitiveIndex, leftObject.transformIndex);

      if (tempHit.isHit) {
        hit = tempHit;
      }
    }

    objIndex = curNode.rightObjIndex;
    if (objIndex >= 1u) {
      Object rightObject = objects[objIndex - 1u];
      HitRecord tempHit = hitPrimitiveBvh(r, dirMin, hit.dir, rightObject.firstBvhIndex, rightObject.firstPrimitiveIndex, rightObject.transformIndex);

      if (tempHit.isHit) {
        hit = tempHit;
      }
    }

    uint leftNodeIndex = curNode.leftNode;
    uint rightNodeIndex = curNode.rightNode;

    float leftDist = 1000000000000.0f;
    float rightDist = 1000000000000.0f;

    if (leftNodeIndex > 0u) {
      BvhNode leftNode = objectBvhNodes[leftNodeIndex - 1u];
      leftDist = intersectAABB(r, leftNode.minimum, leftNode.maximum);
    }

    if (rightNodeIndex > 0u) {
      BvhNode rightNode = objectBvhNodes[rightNodeIndex - 1u];
      rightDist = intersectAABB(r, rightNode.minimum, rightNode.maximum);
    }

    if (leftDist >= 1000000000000.0f) {
      if (rightDist < 1000000000000.0f) {
        stack[stackIndex] = rightNodeIndex;
        stackIndex++;
      }

      continue;
    }

    if (rightDist >= 1000000000000.0f) {
      stack[stackIndex] = leftNodeIndex;
      stackIndex++;
      continue;
    }

    if (leftDist <= rightDist) {
      stack[stackIndex] = rightNodeIndex;
      stackIndex++;

      stack[stackIndex] = leftNodeIndex;
      stackIndex++;
    } else {
      stack[stackIndex] = leftNodeIndex;
      stackIndex++;

      stack[stackIndex] = rightNodeIndex;
      stackIndex++;
    }
  }

  return hit;
}

void main() {
  RayData rayData = objectRayBuffer.rayDatas[gl_GlobalInvocationID.x];
  HitRecord hitRecord;
  
  hitRecord.isHit = false;
  hitRecord.hitIndex = 0u;
  hitRecord.materialIndex = 0u;
  hitRecord.point = vec3(0.0f);
  hitRecord.dir = vec3(0.0f);
  hitRecord.normal = vec3(0.0f);
  
  if (length(rayData.ray.direction) > 0.1f) {
    hitRecord = hitObjectBvh(rayData.ray, rayData.dirMin, rayData.dirMax);
  }

  hitRecord.rayBounce = rayData.rayBounce;
  objectHitBuffer.records[gl_GlobalInvocationID.x] = hitRecord;
}